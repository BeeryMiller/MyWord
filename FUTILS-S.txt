KSCAN  DATA UTILWS,KSCAN0
VSBW   DATA UTILWS,VSBW0
VMBW   DATA UTILWS,VMBW0
VSBR   DATA UTILWS,VSBR0
VMBR   DATA UTILWS,VMBR0
VWTR   DATA UTILWS,VWTR0
       DATA DLNKWS,DLENTR     DSRLNK ENTRY POINTER
       B    @EDIT             switch to text edit entry
       B    @SAVDEF           save defaults fast
       B    @PROG             switch to regular editor
VMBWD  DATA UTILWS,VMBWD0     VMBW routine with data statement
GETPTR DATA WS,GETPT0
DOKEYS DATA WS,DOKEY0
       DATA WS,SAVUSR
*
UTILWS BSS  32
*AK     BSS  2
AR     EQU  2*2+1+UTILWS LOW BYTE OF R2 OF UTILWS

KSCAN0
       LWPI >83E0
*      MOV  R11,@AK
       BL   @>000E
       LWPI UTILWS
       LIMI 2
       LIMI 0                 CAN THE INTERRUPTS
*      MOV  R11,@>83F6
       RTWP
*
*      in: R0 is KB number as a word
*      out: R0<>0 if new key
*           R1 msb is keycode
*
DOKEY0 MOV  *R13,R0
       SWPB R0
       MOVB R0,@>8374         install KB number from users R0
*
       LWPI >83E0
       BL   @>E
       LWPI WS
       CLR  *R13
       MOVB @>837C,*R13       copy status byte....
       MOVB @>8375,@2(R13)    return keypress in R1 msb
       RTWP                   return
*
*
VSBW0
       BL   @AM
       MOVB @>0002(R13),@>8C00
       RTWP

VMBWD0 MOV  *R14+,R2          get VDP location
       MOVB @AR,@>8C02
       ORI  R2,>4000
       MOVB R2,@>8C02
       MOV  *R14+,R1          get CPU location
       MOV  *R14+,R2          get length
       JMP  AN
VMBW0
       BL   @AM
AN     MOVB *R1+,@>8C00
       DEC  R2
       JNE  AN
       RTWP

VSBR0
       BL   @AO
       MOVB @>8800,@>0002(R13)
       RTWP

VMBR0
       BL   @AO
AP     MOVB @>8800,*R1+
       DEC  R2
       JNE  AP
       RTWP

VWTR0
       MOV  *R13,R1
       MOVB @>0001(R13),@>8C02
       ORI  R1,>8000
       SWPB R1
       SWPB R1
       MOVB R1,@>8C02
       RTWP

AM     LI   R1,>4000
       JMP  AQ
AO     CLR  R1
AQ     MOV  *R13,R2
       MOVB @AR,@>8C02
       SOC  R1,R2
       MOVB R2,@>8C02
       MOV  @>0002(R13),R1
       MOV  @>0004(R13),R2
       B    *R11

